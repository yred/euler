-- Problem 45 - Triangular, pentagonal, and hexagonal
--
-- Triangle, pentagonal, and hexagonal numbers are generated by the following
-- formulae:
--
-- Triangle        T(n) = n(n+1)/2     1, 3, 6, 10, 15, ...
-- Pentagonal      P(n) = n(3n−1)/2    1, 5, 12, 22, 35, ...
-- Hexagonal       H(n) = n(2n−1)      1, 6, 15, 28, 45, ...
--
-- It can be verified that T(285) = P(165) = H(143) = 40755.
--
-- Find the next triangle number that is also pentagonal and hexagonal.


main = putStrLn $ show solution

solution :: Integer
solution = head $ foldl1 intersect [ts, ps, hs] 
    where
        ts = drop 285 triangles
        ps = drop 165 pentagonals
        hs = drop 143 hexagonals

triangles :: [Integer]
triangles = map (\n -> n*(n+1) `div` 2) [1..]

pentagonals :: [Integer]
pentagonals = map (\n -> n*(3*n-1) `div` 2) [1..]

hexagonals :: [Integer]
hexagonals = map (\n -> n*(2*n-1)) [1..]

-- Assumption: both `xs` and `ys` are increasing sequences
intersect :: Ord a => [a] -> [a] -> [a]
intersect xs@(x:x') ys@(y:y')
    | x == y    = x : intersect x' y'
    | x > y     = intersect xs y'
    | otherwise = intersect x' ys
